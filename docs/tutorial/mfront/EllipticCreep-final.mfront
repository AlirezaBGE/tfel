@Parser    Implicit;
@Behaviour EllipticCreep;
@Author    Maxime Salvo / Thomas Helfer;
@Date      9 Octobre 2013;

@Includes{
#include"TFEL/Material/Lame.hxx"
}

@Epsilon 1.e-11;

// equivalent hydrostatic viscoplastic strain
@StateVariable real pv;
pv.setEntryName("HydrostaticEquivalentViscoplasticStrain");
// equivalent deviatoric viscoplastic strain
@StateVariable real pd;
pd.setEntryName("DeviatoricEquivalentViscoplasticStrain");
// porosity
@StateVariable real f;
f.setGlossaryName("Porosity");

@Parameter A = 8.e-67;
A.setEntryName("CreepCoefficient");
@Parameter E = 8.2;
E.setEntryName("CreepExponent");
@Parameter CAf = 1;
CAf.setEntryName("CAf");

// Coefficient de Poisson
@LocalVariable real nu;
// Premier coefficient de Lamé
@LocalVariable stress lambda;
// Second coefficient de Lamé
@LocalVariable stress mu;

@MaterialLaw "UO2_YoungModulus_Fink1981.mfront";
@MaterialLaw "UO2_PoissonRatio_Fink1981.mfront";

@ComputeThermalExpansion "UO2_ThermalExpansion_MATPRO.mfront";

@InitLocalVariables{
  /* Initialize Poisson coefficient */
  nu    = UO2_PoissonRatio_Fink1981();
} // end of @InitLocalVars

@ComputeStress{
  using namespace tfel::material::lame;
  // evaluate young modulus to take porosity variation into account
  const stress young = UO2_YoungModulus_Fink1981(T,min(max(f,real(0)),real(1)));
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
  sig = lambda*trace(eel)*Stensor::Id()+2*mu*eel;
} // end of @ComputeStresss

@Integrator{
  // porosity
  const real f_ = min(max(f+theta*df,real(0)),real(1));
  ff       = df*(1+theta*dpv)-(1-f)*dpv;
  dff_ddf  = (1+theta*dpv);
  dff_ddpv = -1+f_;
  // hydrostatic pressure
  const stress pr  = trace(sig)/real(3);
  // Von Mises stress
  const stress seq = sigmaeq(sig);
  // equivalent stress
  const real Af = 2.25*CAf*pow(E*(pow(f_,-E)-1),-2*E/(E+1));
  const real Bf = (1+2*f_/real(3))*pow(1-f_,-2*E/(E+1));
  const stress s  = sqrt(Af*pr*pr+Bf*seq*seq);
  if(s>1.e-8*mu){
    // normal
    real inv_seq(0);
    Stensor n(real(0.));
    if(seq > 1.e-8*mu){
      inv_seq = 1/seq;
      n       = 1.5*deviator(sig)*inv_seq;
    }
    const real tmp           =  A*pow(s,E-1);
    const real ds_dpr        =  Af*pr/s;
    const real ds_dseq       =  Bf*seq/s;
    const real d2s_dpr2      =  Af*Bf*seq*seq/(s*s*s);
    const real d2s_dseq2     =  Af*Bf*pr *pr /(s*s*s);
    const real d2s_dprdseq   = -Af*Bf*pr *seq/(s*s*s);
    const real dphi_ds       =  tmp*s;
    const real d2phi_ds2     =  E*tmp;
    const real dphi_dp       =  dphi_ds*ds_dpr;
    const real dphi_dseq     =  dphi_ds*ds_dseq;
    const real d2phi_dp2     =  d2phi_ds2*ds_dpr*ds_dpr  +dphi_ds*d2s_dpr2;
    const real d2phi_dpdseq  =  d2phi_ds2*ds_dpr*ds_dseq +dphi_ds*d2s_dprdseq;
    const real d2phi_dseq2   =  d2phi_ds2*ds_dseq*ds_dseq+dphi_ds*d2s_dseq2;
    // hydrostatic part
    const real K = lambda+(2*mu)/3;
    fpv        -= dphi_dp*dt;
    dfpv_ddeel -= theta*dt*(d2phi_dp2*K*Stensor::Id()+2*mu*d2phi_dpdseq*n);
    // deviatoric part
    fpd        -= dphi_dseq*dt; 
    dfpd_ddeel -= theta*dt*(2*mu*d2phi_dseq2*n+K*d2phi_dpdseq*Stensor::Id());
    // elasticity
    feel        += (dpv/3)*Stensor::Id()+dpd*n;
    dfeel_ddeel += 2.*mu*theta*dpd*inv_seq*(Stensor4::M()-(n^n));
    dfeel_ddpv   = (real(1)/3)*Stensor::Id();
    dfeel_ddpd   = n;
  }
  feel -= deto;
} // end of @Integrator

@PredictionOperator{
  using namespace tfel::material::lame;
  if(smt==ELASTIC){
    computeAlteredElasticStiffness<hypothesis,Type>::exe(Dt,lambda,mu);
  } else {
    return FAILURE;
  }
}

@TangentOperator{
  using namespace tfel::material::lame;
  if((smt==ELASTIC)||(smt==SECANTOPERATOR)){
    computeAlteredElasticStiffness<hypothesis,Type>::exe(Dt,lambda,mu);
  } else if (smt==CONSISTENTTANGENTOPERATOR){
    Stensor4 Je;
    getPartialJacobianInvert(Je);
    StiffnessTensor De;
    computeElasticStiffness<N,Type>::exe(De,lambda,mu);
    Dt = De*Je;
  } else {
    return false;
  }
}
