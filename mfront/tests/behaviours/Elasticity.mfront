@Behaviour Elasticity;
@Author Helfer Thomas;
@Date 23/11/06;
@Description{
 A very first test 
 (the most simple one in fact).
}

@ModellingHypotheses {".+"};

@UseQt true;
@ProvidesSymmetricTangentOperator;

@UMATFiniteStrainStrategies[umat] {None,FiniteRotationSmallStrain,
                                   MieheApelLambrechtLogarithmicStrain,
                                   LogarithmicStrain1D};

@AsterSaveTangentOperator[aster]        true;
@AsterGenerateMTestFileOnFailure[aster] true;

// Material properties
// rho and alpha are mandatory
// for the Cast3M finite element solver
@MaterialProperty stress young;
@MaterialProperty real   nu;
young.setGlossaryName("YoungModulus");
nu.setGlossaryName("PoissonRatio");

// Lame Coefficients
@LocalVariable stress lambda,mu;

@Includes{
#include"TFEL/Material/Lame.hxx"
}

@InitLocalVariables{
  using namespace tfel::material::lame;
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
}

@PredictionOperator{
  using namespace tfel::material::lame;
  static_cast<void>(smt); // remove unused variable warning
  computeAlteredElasticStiffness<hypothesis,real>::exe(Dt,lambda,mu);
}

@Integrator{
  sig = lambda*trace(eto+deto)*StrainStensor::Id()+2*mu*(eto+deto);
  if(computeTangentOperator_){
    Dt = lambda*Stensor4::IxI()+2*mu*Stensor4::Id();
  }
}

@Integrator<PlaneStress,AxisymmetricalGeneralisedPlaneStress,Replace>{
  using namespace tfel::material::lame;
  // here we compute the tangent operator even if we don't need too
  static_cast<void>(computeTangentOperator_);
  computeAlteredElasticStiffness<hypothesis,real>::exe(Dt,lambda,mu);
  sig = Dt*(eto+deto);
}
